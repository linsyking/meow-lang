true() {"T"}

false() {"F"}

zero() {"0"}

succ(x) {"S"+x}

pred(x) {
    "S0" = "0";
    x
}

add (x,y) {
    "0" = y;
    x
}

sub (x,y) {
    y = "0";
    x
}

mul(x,y) {
    "S" = {"0"=""; y};
    x
}

dr(x,y,z) {
    y = x;
    x = y;
    z
}

encode(s) {
    var rep = {
        "$" = "\$";
        "^" = "\^";
        "\" = "\\";
        s
    };
    "_^"+ rep +"_$"
}

decode(s) {
    var rep = {
        "_^" = "";
        "_$" = "";
        s
    };
    "\\" = "\";
    "\$" = "$";
    "\^" = "^";
    rep
}

eq(x,y) {
    encode(x) = "F";
    encode(y) = "T";
    encode(x)
}

if(c,x,y) {
    var cc = c + "$";
    var enc = {
        "T$" = encode(x);
        "F$" = encode(y);
        cc
    };
    decode(enc)
}

in(x,y) {
    not(eq(y,{x=""; y}))
}

not(x) {
    if(x, false(), true())
}

ife(x) {
    eq(x,"")
}

digit(x) {
    "1" = "S0";
    "2" = "SS0";
    "3" = "SSS0";
    "4" = "SSSS0";
    "5" = "SSSSS0";
    "6" = "SSSSSS0";
    "7" = "SSSSSSS0";
    "8" = "SSSSSSSS0";
    "9" = "SSSSSSSSS0";
    x
}

exp(x,y) {
    var sm = {
        " 0" = "";
        "S" = `"`+x+`" `;
        y
    };
    var pre = {
        "S" = "mul ";
        "S0" = "";
        y
    };
    !(pre + sm)
}

nrep(x,p) {
    "0" = p;
    "1" = p;
    "2" = p;
    "3" = p;
    "4" = p;
    "5" = p;
    "6" = p;
    "7" = p;
    "8" = p;
    "9" = p;
    x
}

len(x) {
    var s = nrep(x, "S");
    s + "0"
}

ten() {
    succ(digit("9"))
}

revdigit(x) {
    "S0" = "1";
    "SS0" = "2";
    "SSS0" = "3";
    "SSSS0" = "4";
    "SSSSS0" = "5";
    "SSSSSS0" = "6";
    "SSSSSSS0" = "7";
    "SSSSSSSS0" = "8";
    "SSSSSSSSS0" = "9";
    x
}

head(x) {
    var s = {
        "$0" = "S";
        "$1" = "SS";
        "$2" = "SSS";
        "$3" = "SSSS";
        "$4" = "SSSSS";
        "$5" = "SSSSSS";
        "$6" = "SSSSSSS";
        "$7" = "SSSSSSSS";
        "$8" = "SSSSSSSSS";
        "$9" = "SSSSSSSSSS";
        "$" + x
    };
    var rep = nrep(s, "");
    pred(rep + "0")
}

num(x) {
    var nl = pred(len(x));
    var hd = head(x);
    var hdnum = revdigit(hd);
    var remain = {
        "$" + hdnum = "";
        "$" + x
    };
    !(if(
        eq(len(x), "S0"),
        spack(digit(x)),
        `add mul "` + hd + `" exp ten "` + nl + `" ` + `num "` + remain + `"`
    ))
}

spack(x) {
    `"` + x + `"`
}

leq(x,y) {
    in(x,y)
}

show(x) {
    var la = {
        "SSSSSSSSSS" = "";
        x
    };
    var rem = {
        "SSSSSSSSSS" = "S";
        sub(x,la)
    };
    !(if(
        leq(len(x), digit("9")),
        spack(revdigit(x)),
        `cat show "` + rem + `" revdigit "` + la + `"`
    ))
}

fib(x) {
    if(
        eq(x, zero()),
        zero(),
        !(if(
            leq(x, num("2")),
            spack(num("1")),
            `add fib pred x fib pred pred x`
        ))
    )
}
